plugins {
    id "groovy"
}

group = "com.github.stricklerxc.pipeline"
version = "0.0.1"

repositories {
    maven { url 'https://repo.jenkins-ci.org/releases/' }
    mavenCentral()
}

dependencies {
    implementation "org.codehaus.groovy:groovy-all:2.5.14"

    testImplementation "com.lesfurets:jenkins-pipeline-unit:1.3"
    testImplementation  "junit:junit:4.12"
}

test {
    // delete old test reports
    dependsOn cleanTest

    // don't stop if tests fail
    ignoreFailures = true

    // minimize logging
    testLogging.maxGranularity = 0

    // show stdout from tests
    onOutput { dest, event -> print event.message }

    // show test results
    def results = []
    afterTest { desc, result ->
        println "${desc.className.split("\\.")[-1]}: " +
            "${desc.name}: ${result.resultType}"
    }
    afterSuite { desc, result ->
       if (desc.className) { results << result }
    }

    // show summary
    doLast {
        println "Tests: ${results.sum { it.testCount }}" +
            ", Failures: ${results.sum { it.failedTestCount }}" +
            ", Errors: ${results.sum { it.exceptions.size() }}" +
            ", Skipped: ${results.sum { it.skippedTestCount }}"
    }
}
